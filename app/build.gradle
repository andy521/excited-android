apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'me.tatarka.retrolambda'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git tag'.execute([], project.rootDir).text.trim().split("\n").last()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

def BASE_URL = 'https://excited.aja.im/'
def TEST_URL = 'http://192.168.2.247:3000/'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "ooo.oxo.excited"

        minSdkVersion 21

        targetSdkVersion 23

        versionCode gitCommits
        versionName gitTag

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        addManifestPlaceholders APP_NAME: '@string/app_name'

        buildConfigField "String", "BASE_URL", "\"${BASE_URL}\""
        buildConfigField "String", "TEST_URL", "\"${TEST_URL}\""
    }

    if (project.hasProperty('STOREFILE')) {
        signingConfigs {
            release {
                storeFile file(STOREFILE)
                storePassword STOREPASS
                keyAlias KEYALIAS
                keyPassword KEYPASS
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            addManifestPlaceholders APP_NAME: '@string/app_name_debug'
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (project.hasProperty('STOREFILE')) {
                println "using release keystore ${signingConfigs.release.storeFile}"
                signingConfig signingConfigs.release
            } else {
                println "using default key"
            }
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    productFlavors {
        github {
            manifestPlaceholders = [UMENG_CHANNEL: "GitHub"]
        }

        google {
            manifestPlaceholders = [UMENG_CHANNEL: "Google"]
        }

        coolapk {
            manifestPlaceholders = [UMENG_CHANNEL: "CookApk"]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                output.outputFile = file("${output.outputFile.parent}/excited" +
                        "-${variant.buildType.name.toLowerCase()}" +
                        "-${variant.versionName}" +
                        "-${variant.flavorName.toLowerCase()}" +
                        ".apk")
            }
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

ext{
    supportLibrary = '25.1.0'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:${supportLibrary}"
    compile "com.android.support:design:${supportLibrary}"
    compile "com.android.support:customtabs:${supportLibrary}"
    compile 'com.roughike:bottom-bar:2.0.2'

    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.trello:rxlifecycle:0.7.0'
    compile 'com.trello:rxlifecycle-components:0.7.0'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
    compile 'com.squareup.sqlbrite:sqlbrite:1.1.0'

    compile 'com.facebook.rebound:rebound:0.3.8'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'me.drakeet.multitype:multitype:2.3.0'
    compile 'com.daimajia.numberprogressbar:library:1.2@aar'

    compile 'ooo.oxo:touch-image-view:1.0.1'

    compile 'com.umeng.analytics:analytics:latest.integration'

    compile 'commons-io:commons-io:2.5'

    compile project(':crosswalk')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}